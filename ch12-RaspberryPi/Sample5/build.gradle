plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.hidetake.ssh' version '2.11.2'
}

repositories {
    mavenCentral()
}

application {
    mainModule = 'hellofx'
    mainClass = 'org.modernclients.raspberrypi.MainApp'
}
mainClassName = "hellofx/org.modernclients.raspberrypi.MainApp"

javafx {
    version = '23-ea+22'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

def workingDir = '/home/pi/ModernClients/ch12-RaspberryPi/Sample5'
def javaHome = '/home/pi/.sdkman/candidates/java/current'
def javafxHome = '/opt/javafx-sdk-23/lib'

task libs(type: Copy) {
    dependsOn 'jar'
    into "${buildDir}/libs/"
    from configurations.compileClasspath
}

remotes {
    pi23 {
        host = 'raspberrypi.local'
        user = 'pi'
        password = 'pi'
    }
}

task runRemoteEmbedded {
    dependsOn 'libs'
    ssh.settings {
        knownHosts = allowAnyHosts
    }
    doLast {
        ssh.run {
            session(remotes.pi23) {
                execute "mkdir -p ${workingDir}/dist"

                fileTree("${buildDir}/libs")
                        .filter { it.isFile() && ! it.name.startsWith('javafx')}
                        .files
                        .each { put from:it, into: "${workingDir}/dist/${it.name}"}

                executeSudo "-E ${javaHome}/bin/java -Dfile.encoding=UTF-8 " +
                        "--module-path=${javafxHome}:${workingDir}/dist " +
                        "-Dmonocle.platform=EGL -Dembedded=monocle -Dglass.platform=Monocle " +
                        "-Dmonocle.egl.lib=${javafxHome}/libgluon_drm-1.1.7.so " +
                        "-m ${project.mainClassName}"
            }
        }
    }
}
